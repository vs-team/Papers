%Casanova 2 is an iteration of Casanova, a DSL for game development. A Casanova program is a tree of data structures called \textit{entities}. The root of the tree is called \textit{world}. Each entity contains a set of \textit{rules} which modifies the fields and describe continuous dynamics. Discrete dynamics (the dynamics of the game that require timing or synchronization conditions) are expressed by coroutines implemented with a variation of the state monad. 
Casanova 2 eliminates this separation by allowing rule to be interruptible, thus describing both continuous and discrete dynamics. The rule body is looped continuously, which means that once the evaluation reaches the end, the rule is re-started from the first statement. Writing entity fields is allowed only by using rules. A rule can write an entity field by using a dedicated \texttt{yield} statement. Each rule declares a subset of fields called \textit{domain} on which it is allowed to write. Besides each rule has an implicit reference to the world (variable \texttt{world}), the current entity (variable \texttt{this}), and the time difference between the current frame and the previous (variable \texttt{dt}). Casanova 2 supports interruptible control structures and queries, so it natively supports REA.

\section{Galaxy Wars in Casanova 2}
We now show that Casanova 2 is able to express the design of Galaxy Wars in terms of REA. In what follows the type $[T]$ denotes a list of objects of type $T$, according to Casanova 2 syntax. We begin by defining the structure of the world entity. The world contains the collection of \texttt{Planets} in the map, the collection of \texttt{Links} connecting the planets, the collection of \texttt{Players}, and a \texttt{Controller} that manages the input controller and provides facilities like: the current selected planet, whether a mouse button is down, etc.

\begin{lstlisting}
worldEntity GalaxyWars =
  Planets    : [Planet]
  Links      : [Link]
  Players    : [Player]
  Controller : Controller
  
  //rules
\end{lstlisting}



\subsection{Resources}
The resources are all those elements that influence the game dynamics. In Galaxy Wars the resources are: 

\begin{itemize}[noitemsep]
\item the players statistics (attack, defense, production, research)
\item the planets statistics
\item the fleets statistics
\item the fleets stationed in a planet
\item the fleets moving around the map
\end{itemize}

\noindent
We use the below properties to model the statistics of the entities: player, planet, and fleet. We use these statistics to amplify or reduce the amount of resources to transfer, thus to alter the impact of the effects of the entity container.
\begin{lstlisting}
entity GameStatistics =
  Attack               : float32
  Defence              : float32
  Production           : float32
  Research             : float32
\end{lstlisting}



\subsection{Entities}
\noindent
Entities represent the resource containers in Galaxy Wars. The entities in Galaxy Wars are: 

\noindent
\newline
\textbf{Planet}, which represents the container of stationed fleets. Each planet has its own statistics. Statistics that affect: the attack and velocity of outgoing fleets; the local production of fleets; and the defense capabilities

\noindent
\newline
\textbf{Link}, which is a directed connection between two planets. Links are used by fleets to move around the map	

\noindent
\newline
\textbf{Fleet}, which represents the armies of a player. A fleet is made up by ships, and to every fleet statistics are assigned. A fleet might behave differently depending on its current task. Reason why we distinguish different kind of fleet entity, each of which inherits a fleet and is able to accomplish a specific task. The kind of fleets that we identified are:
		\begin{itemize}[noitemsep]
			\item \textbf{AttackingFleet}, which is a fleet capable to carry out fighting tasks
			\item \textbf{AttackingFleetToMerge}, which represents a special attacking fleet which has just conquered a planet and thus has to be added to the planet stationary fleets (together with the other allied attacking fleets who that attacking the planet)
			\item \textbf{TravelingFleet}, which represents a fleet traveling along a link
			\item \textbf{LandingFleet}, which represents a special traveling fleet which is about to land on the destination planet
		\end{itemize}
		
\noindent
\newline
\textbf{Battle}, which carries out the fighting task on a planet 

\noindent
\newline
\textbf{Player}, which is the owner of entities in game. Every player belongs to a faction. Factions differ among each other based on their statistics. During the game statistics of a player can be changed by means of upgrades.


\subsection{Fields}
In addition to the resources defined above, additional data fields are used in every entity to support the internal logic of each entity. In what follows we go through each entity and for each entity a code listing the fields and brief description is provided.

\paragraph{Planet}
Each planet got: its own statistics, the amount of stationed fleets, the incoming fleets, an owner, a link to a possible battle, and the landing fleets.
\begin{lstlisting}
entity Planet =
  Statistics    : GameStatistic
  LocalFleets   : int
  InboundFleets : [Fleet]
  ref Owner     : Option<Player>
  Battle        : Option<Battle>
  LandingFleets : [LandingFleet] 
\end{lstlisting}
\paragraph{Link}
Besides its source and destination, a link made up of a collection of traveling fleets.
\begin{lstlisting}
entity Link =
  ref Source        : Planet
  ref Destination   : Planet
  TravellingFleets  : [TravellingFleet]
\end{lstlisting}
\paragraph{Fleet}
A \texttt{Fleet} is made of: statistics, the amount of ships, a ref to the link on which its is traveling, an owner, a destroyed flag, and the position.
\begin{lstlisting}
entity Fleet =
  Statistics  : GameStatistic
  Ships       : int
  ref Link    : Link
  ref Owner   : Player
  Destroyed   : bool
  Position    : Vector3
\end{lstlisting}
\subparagraph{AttackingFleet}
An attacking fleet is a specialized fleet that contains: a ref to the actual fleet and a reference to its battle.
\begin{lstlisting}
entity AttackingFleet =
  ref MyFleet : Fleet
  ref MyBattle : Battle
\end{lstlisting}

\subparagraph{AttackingFleetToMerge}
An attacking fleet to merge is a specialized fleet that contains: a ref to the actual fleet and a reference to the attacking fleet with which it has to join.
\begin{lstlisting}
entity AttackingFleetToMerge =
  ref MyFleet : Fleet
  ref FleetToMergeWith : AttackingFleet
\end{lstlisting}

\subparagraph{TravelingFleet}
Is a specialized fleet that contains: a reference to the actual fleet, the destination planet, and the velocity.
\begin{lstlisting}
entity TravelingFleet =
  MyFleet : Fleet
  ref Destination : Planet
  Velocity : Vector3
\end{lstlisting}

\subparagraph{LandingFleet}
Contains the reference to the actual fleet.
\begin{lstlisting}
entity LandingFleet =
  MyFleet : Fleet
\end{lstlisting}

\paragraph{Battle}
A battle is made up of:the planet where the battle is hosted, a collection of attacking fleets, the possible amount of losses of the hosting planet, the possible amount of losses of the attacking fleets, the just destroyed attacking fleets, and the just arrived attacking fleets that have to be grouped into the attacking fleets.


\begin{lstlisting}
entity Battle =
  ref MySource    : Planet
  AttackingFleets : [AttackingFleet]
  DefenceLost     : Option<int>
  AttackLost      : Option<int>
  FleetsToDestroyNextTurn  : [AttackingFleet]
  FleetsToMerge   : [AttackingFleetToMerge]
\end{lstlisting}

\paragraph{Player}
A player is made of the statistics of its faction and the its name.
\begin{lstlisting}
entity Player =
  Statistics : GameStatistic
  Name : string
\end{lstlisting}

\subsection{Actions}
Actions are the only way, according to REA, to exchange resources like the amount of attacks in a battle, the amount of fleets to produce, etc. In Galaxy Wars we identified three kind of actions: battle, production, and upgrade.
\\\\
\noindent
\subsubsection{Battle}
\noindent
A Battle action involves a planet \texttt{MySource} and a series of \texttt{AttackingFleets}.
\paragraph{Attack} In this design only one selected attacking fleet at a time can attack \texttt{MySource}, precisely the fleet which is in the head of the \texttt{AttackingFleets} collection. Every random time an amount of damage is computed and stored in the \texttt{Battle} entity. Before computing the amount of damage, we check that there are still fleets in the \texttt{AttackingFleets} collection.

\begin{lstlisting}
entity Battle =
  ...
  rule AttackLost, DefenceLost =
    yield None, None
    wait 1.0f
    if AttackingFleets.Count > 0 then
        yield // amount of looses based on the  
              // statistics of both the attacking 
              // fleet and the planet	   
\end{lstlisting}

The amount of damage represents the damage that has be applied to both: the selected attacking fleet and the defending planet. This damage will always be applied since every instance of \texttt{AttackingFleet} and \texttt{Planet} involved in a battle keep updating their amount of fleets.


\begin{lstlisting}
entity AttackingFleet =
  ...
  rule MyFleet.Ships =
    wait MyBattle.AttackLost.IsSome && MyBattle.AttackingFleets.Head = this
    yield MyFleet.Ships - MyBattle.AttackLost
    
entity Planet =
  ...
  rule LocalFleets =
    wait Battle.IsSome && Battle.DefenceLost.IsSome
    yield LocalFleets - Battle.DefenceLost.Value
\end{lstlisting}

\paragraph{Attacking fleet selection}
A random circularly selection based on random time is used to allow all attacking fleets to attack the planet.
\begin{lstlisting}
entity Battle =
  ...
  rule AttackingFleets =
    .| AttackingFleets.Count <= 1 => yield AttackingFleets
    .| _ =>
      wait Random.Range(1.0f, 2.0f)
      yield AttackingFleets.Tail @ [AttackingFleets.Head]
\end{lstlisting}

\paragraph{Ownership}
We change the owner of a planet when at the end of a battle the attacker list is not empty. When we change the owner we also update the amount of \texttt{LocalFleet}, by summing all the fleets that share the same new owner and that are attacking the planet.
\begin{lstlisting}
entity Planet =
  ...
  rule Owner, LocalFleets =
    if Battle.IsSome &&
       LocalFleets = 0 &&
       Battle.AttackingFleets.Count > 0 then
    let new_owner = Battle.AttackingFleets.Head.MyFleet.Owner
    let fleets_to_add =
	  Battle.AttackingFleets
	  .Where(f => f.MyFleet.Owner = new_owner && f.MyFleet.Ships > 0)
	  .sum(f => f.MyFleet.Ships)
    yield Some new_owner, fleets_to_add 
\end{lstlisting}


\subsubsection{Production}
The spawning of a new fleet follows the following schema: if a battle is ongoing on a planet then the production is interrupted and the planet keeps polling the battle in order to update its local fleets; if the planet is neutral (it is not possessed by any player) then production is interrupted; eventually if the planet is not neutral and the is not an ongoing battle then we wait some time, which depends on the production statistics of both the player and the planet, and then we add a new fleet to the local fleets.
\begin{lstlisting}
entity Planet =
  ...
  rule Owner, LocalFleets =
    .| Battle.IsSome => yield LocalFleets
    .| Owner.IsNone => yield 0
    .| _ =>
      wait //time depending on the owner
           //and the planet production
      yield LocalFleets + 1
\end{lstlisting}


\subsubsection{Upgrade} When the planet is selected and a key associated to an upgrade is down we: (\textit{i}) wait an amount of time (which depends from the owner and the planet research statistics), and then (\textit{ii}) we upgrade the selected statistic. If the planet is neutral less then its statistics are, by default, set to \texttt{1}.

\begin{lstlisting}
entity Planet = 
  ...
  rule Statistics.STAT =
    .| Owner.IsNone -> yield 1
    .| _ ->
      wait IsSelected && KeyPressed(STAT_KEY)
      wait //time depending on the owner
           //and the planet research
      yield max(MAX_STAT, Statistics.STAT + 1)
\end{lstlisting}


\subsubsection{Creation}
In Galaxy Wars we create entities when: (\textit{i}) a battle is about to start, and (\textit{ii}) when a fleet is spawned.

\paragraph{Battle} On a planet a battle is created either when either the planet is neutral and a fleet is approaching the planet; or the planet is not neutral, there are no battles ongoing on the planet, and an enemy fleet is approaching the planet.
\begin{lstlisting}
entity Planet =
  ...
  rule Battle =
	let exits_an_enemy_fleet = 
	  LandingFleets.Count = InboundFleets.Count |> not
    if (Owner.IsNone && Battle.IsNone && exits_an_enemy_fleet) ||
       (Owner.IsSome && exits_an_enemy_fleet) then
      yield Some (new Battle(this))
      wait Battle.AttackingFleets.Count <= 0
    yield None
\end{lstlisting}


\paragraph{Fleet}
We consume all local fleets of a planet and move them through the link when the source planet is selected (and its fleets are greater than 0) and the destination planet is selected as well. The selection logic of planets is in the planet entity and it is not covered in this presentation.

\begin{lstlisting}
entity Link =
  ...
  rule TravellingFleets, Source.LocalFleets = 
    wait Source.Selected && Destination.RightSelected &&
         Source.Owner.IsSome && Source.Battle.IsNone &&
         Source.LocalFleets > 0		
    yield new TravellingFleet(Destination) :: TravellingFleets, 0
\end{lstlisting}

\subsection{Deletion}
\noindent
Analogously to creation, in GW the entities which might be disposed during a game are battles and fleets. 


\paragraph{Battle}
The logic of the deletion of a battle is tightly related to the logic of its creation. In the previous subsection a battle is disposed only when the amount of \texttt{AttackingFleets} is equals to \texttt{0}.

\paragraph{Fleet}
The general logic of the deletion of a fleet follows the following criteria: if the fleet got no ships then it has to be destroyed. In code, a fleet destroys itself when the amount of its \texttt{Ships} is less or equals to zero.
\begin{lstlisting}
entity Fleet =
  ...
  rule Destroyed = 
    wait Ships <= 0
    yield true
\end{lstlisting}

\noindent
Special attention goes to when the ship is: fighting or about to land or traveling.

\subparagraph{Fighting} If during a battle the attacker manages to conquer the planet then all the attacking fleets that share the same owner of the just conquered planet have to be destroyed. 
\begin{lstlisting}
entity AttackingFleet =
  ...
  rule MyFleet.Destroyed =
    wait (MyBattle.MySource.Owner.IsSome && 
          MyFleet.Owner = MyBattle.MySource.Owner)
    yield true
\end{lstlisting}
And filtered from the attacking fleets collection and moved into. We move such fleets to destroy in a different collection so their logic will not affect the logic of the battle. Entities in such collection last for one frame.
\begin{lstlisting}
entity Battle =
  ...
  rule FleetsToDestroyNextTurn =
    yield
      [for f in AttackingFleets do       
       where (MySource.Owner.IsSome && f.MyFleet.Owner = MySource.Owner))
       select f]
\end{lstlisting}
Fleets that have not managed to conquer the planet, but which have been destroyed are filtered from the attacking list collection.
\begin{lstlisting}
entity Battle =
  ...
  rule AttackingFleets =
    yield 
      [for f in AttackingFleets do       
       where (not f.MyFleet.Destroyed) &&
             not FleetsToDestroyNextTurn.Contains(f)
       select f]
\end{lstlisting}


\subparagraph{Landing}
A landing fleet is a traveling fleet, which is about to land and whose owner is the same as its destination planet. In order to not to add twice the ships of the landing fleet among the local fleets of the destination planet, a landing fleet lasts one frame in the game (we destroy it at the first frame).
\begin{lstlisting}
entity LandingFleet
  ...
  rule MyFleet.Destroyed = yield true
\end{lstlisting}

New landing fleets are continuously added to the local fleets.
\begin{lstlisting}
entity Planet
  ...
  rule LocalFleets =
    yield LandingFleets.Sum(f => f.MyFleet.Ships) +
          LocalFleets
\end{lstlisting}

\subparagraph{Traveling}
When a traveling fleet has reached its destination, the fleet is automatically filtered by the link. 
\begin{lstlisting}
entity Link =
  ...
  rule TravellingFleets =
  yield 
    [for f in TravellingFleets do
     where Vector3.Distance(f.MyFleet.Destroyed |> not &&
                            f.MyFleet.Position, Destination.Position) > Destination.MinApproachingDist
     select f]
\end{lstlisting}

\subsection{Behavior update}
An entity during its life cycle might change its behavior based on its state. An example of this kind of behavior in GalaxWars could be identified in the fleet entity. For example an attacking fleet behaves differently than a moving fleet. In Casanova we distinguish these two cases by mean of two different entities that share some common properties, but implement different rules.

\paragraph{InboundFleet}
When a fleet, traveling along a link, is approaching the destination, the planet has to choose whether to: (\textit{i}) add the fleet to the planet local fleets (see production of a planet above), or (\textit{ii}) add the fleet to a battle. To implement the just described scenario we start with the definition of a buffer to place in the \texttt{Planet} entity called \texttt{InboundFleets}. The \texttt{InboundFleets} of a planet represents all fleets that are approaching at a specific time the planet. 

\begin{lstlisting}
entity Planet =    
  ...  
  rule InboundFleets =
    yield [for l in world.Links do     
           where (l.Destination = this)
           for f in l.TravellingFleets do
           where (Vector3.Distance(f.MyFleet.Position, Position) <= MinApproachingDist)
           select f.MyFleet]
\end{lstlisting}

\texttt{InboundFleets} acts like a dispatcher. When a fleet enters the \texttt{InboundFleets} collection, other entities are able to consume it for their internal logics. To avoid entities to consume twice the same fleet, fleets in \texttt{InboundFleets} last for one frame before being disposed. When an entity consumes an inbound fleet it decides what behaviors to apply to the selected fleet. This is done by assigning the fleet to an other instance, of different type, which contains the fleet but provides new rules.


\paragraph{Attacking fleets to merge}
Fleets that come from the same link and that share the same owner have to e joined. To do so, every enemy inbound fleet that share the same source link of a fleet stored in \texttt{AttackingFleets} is selected and converted into an \texttt{AttackingFleetToMerge}. Eventually all the fleets of type \texttt{AttackingFleetToMerge} are stored into \texttt{FleetsToMerge} for the duration of one frame.
\begin{lstlisting}
entity Battle =
  ...
  rule FleetsToMerge =
    yield
      [for i_f in MySource.InboundFleets do
       for a_f in AttackingFleets do
       where (not a_f.MyFleet.Destroyed &&
              i_f.Link = a_f.MyFleet.Link)
       select (new AttackingFleetToMerge (i_f, a_f))]
\end{lstlisting}

When we create an attacking fleet to merge the reference to the actual attacking fleet is stored in the \texttt{FleetToMergeWith} of the attacking fleet to merge. An attacking fleet every frame iterates through all the attacking fleets of its battle and selects those fleets to merge whose \texttt{FleetToMergeWith} is the attacking fleet. After the selection, the amount of ships of the selected attacking fleets to merger is sum and added to the local fleets of the attacking fleet.
\begin{lstlisting}
entity AttackingFleet =
  ...
  rule MyFleet.Ships =      
    yield MyBattle.FleetsToMerge
           .Where(f => f.FleetToMergeWith = this)
           .sum(f.MyFleet.Ships) 
          + MyFleet.Ships
\end{lstlisting}


\paragraph{Attacking fleet}
A battle entity every frame selects the enemy fleets from the inbound fleets of its \texttt{MySource} field and adds them to its \texttt{AttackingFleets}, as long as the selected fleets are not in \texttt{FleetsToMerge}. Before adding the inbounding attacking fleets, every inbound enemy fleet is converted to an attacking fleet.
\begin{lstlisting}
entity Battle =
  ...
  rule AttackingFleets =
    yield
      [for f in MySource.InboundFleets do
       let is_ship_to_merge = FleetsToMerge.Contains(f)
       where is_ship_to_merge &&
             (MySource.Owner.IsNone || 
              not (f.Owner = MySource.Owner))
       select new AttackingFleet(f, this)]
\end{lstlisting}

\noindent
The moment a fleet becomes an attacking fleet and it is added to the \texttt{AttackingFleet} collection the new logic (the one of the attacking) can be run.

\paragraph{Landing fleet}
A planet every frame selects all allied fleets among its inbound fleets and adds them to the \texttt{LandingFleet} collection, so the planet later can add those fleets to its local fleets. Before adding the inbound allied fleets, every fleets is converted to an inbound fleet.
\begin{lstlisting}
entity Planet
  ...
  rule LandingFleets =
    if Owner.IsSome then
      yield
        [for inbound_fleet in InboundFleets do
         where (inbound_fleet.Owner = Owner)
         select (new LandingFleet(inbound_fleet))]
    else yield []
\end{lstlisting}
