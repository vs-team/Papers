We now give the implementation of the operators seen until now with a simple mutable state.

We define our state as very similar to that of the state monad (that is a statement that evaluates to a value of type $a$ from a state of type $s$ into a state of type $s'$ has the same type of its denotational semantics):
\begin{lstlisting}
data St s s' a = St(s->(a,s'))
\end{lstlisting}

References will be based on the state since a reference must be easily convertible into statements, one for evaluating the reference and one for assigning it:
\begin{lstlisting}
type Get s a = St s s a
type Set s a = a -> St s s ()
\end{lstlisting}

We now need to represent the state (our memory). The simplest implementation of a typed memory is based on heterogeneous lists. A heterogeneous list is build based on two type constructors; one is for the empty list, the other will be given as a concrete constructor for the $Malloc$ type function:
\begin{lstlisting}
data Nil = Nil deriving (Show)

infixr `Malloc`
\end{lstlisting}

Since heterogeneous lists do not have a single type, we characterize all heterogeneous lists with an appropriate predicate:
\begin{lstlisting}
class HList l
instance HList Nil
instance HList tl => HList (Malloc h tl)
\end{lstlisting}

We access heterogeneous lists by index. To ensure type safety we define type-level integers, encoded as Church Numerals:
\begin{lstlisting}
data Z = Z
data S n = S n

class CNum n
instance CNum Z
instance CNum n => CNum (S n)
\end{lstlisting}

We can now read the length of a heterogeneous list, as well as get the type of an arbitrary element of the list:

\begin{lstlisting}
type family HLength l :: *
type instance HLength Nil = Z
type instance HLength (Malloc h tl) = S (HLength tl)

type family HAt l n :: *
type instance HAt (Malloc h tl) Z = h
type instance HAt (Malloc h tl) (S n) = HAt tl n
\end{lstlisting}

We will need a way to manipulate the values of a heterogeneous list. For this reason we define a lookup predicate:
\begin{lstlisting}
class (HList l, CNum n) => HLookup l n where
  lookup :: l -> n -> HAt l n
  update :: l -> n -> HAt l n -> l

instance (HList tl) => HLookup (Malloc h tl) Z where
  lookup (Malloc h tl) _ = h
  update (Malloc h tl) _ h' = (Malloc h' tl)

instance (HList tl, CNum n, HLookup tl n) => HLookup (Malloc h tl) (S n) where
  lookup (Malloc _ tl) _ = lookup tl (undefined::n)
  update (Malloc h tl) _ v' = (Malloc h (update tl (undefined::n) v'))
\end{lstlisting}

Now we have all that we need to instance our memory, reference and state predicates.

We begin by instancing the $Memory$ predicate, since all heterogeneous lists are memory and as such can be used; we 
also define the single concrete constructor for the $Malloc$ datatype, which thus becomes a way to create pairs:
\begin{lstlisting}
instance (HList m) => Memory m where
  data Malloc a m = Malloc a m deriving (Show)
  malloc m a = Malloc a m
  free (Malloc h tl) = tl
  read   (Malloc h tl) = h
  write  h' (Malloc h tl) = Malloc h' tl
\end{lstlisting}

We instance the $Monad$ class with the $St$ type (as in the state monad); because of the restrictions for monads, we can only do so when the input and output states are the same:
\begin{lstlisting}
instance Monad (St s s) where
  return x = St(\s -> (x,s))
  (St st) >>= k = St(\s -> 
                          let (res,s') = st s 
                              (St k') = k res
                          in k' s')
\end{lstlisting}

We also define a way to evaluate a statement and ignoring the resulting state:
\begin{lstlisting}
runSt :: St s s' a -> s -> a
runSt (St st) s = fst (st s)
\end{lstlisting}

Now that $Monad\ (St\ s\ s)$ is instanced we can instance the $State$ predicate for our references and state:
\begin{lstlisting}
instance State St where
  data Ref St m a = StRef (Get m a) (Set m a)
  eval (StRef get set) = get
  (StRef get set) .= v = set v
  delete = St(\s -> ((), free s))
  new v = let new_ref = StRef (St (\s -> (read s, s)))
                              (\v' -> St(\s -> ((), write v' s)))
          in St (\s -> (new_ref, malloc s v))
  (St st) >>>= k = St(\s -> 
                            let (res,s') = st s 
                                (St k') = k res
                            in k' s')
  (St st) >>> (St st') = St(\s -> 
                                 let (res,s') = st s 
                                     (res',s'') = st' s'
                                 in (res,s''))
\end{lstlisting}

Notice that in the above sample a mutable reference is 
simply the pair of a getter and a setter function. Also, the 
$(>>+)$ operator has exactly the same body as that of $(>>=)$ that we have given for monads; this shows that, at 
least when embedding stateful languages inside Haskell, our 
definition is a generalization of the usual state monad.

Thanks to this last instance we can now give a first working example of usage of our references with mutable state:
\begin{lstlisting}
ex1 :: (Monad (st m1 m1), HList m0, m1 ~ (Malloc Int m0), State st) => st m0 m1 Int
ex1 = do+ i <- new 10
          i *= (+2)
          eval i)

res1 = runSt ex1 Nil
\end{lstlisting}

\begin{lstlisting}
ex1' :: (Monad (st m1 m1), HList m0, m1 ~ (Malloc Int m0), State st) => st m0 m0 Int
ex1' = do+ i <- new 10
           i *= (+2)
           eval i
           delete

res1' = runSt ex1' Nil
\end{lstlisting}

The result, as expected, is $res1=12$.

We complete the implementation of our system so far by adding records. We use heterogeneous lists to which we access via labels. A label is defined with a getter and a setter (similar to those found in the $Ref$ constructor). We can instance the $Record$ predicate:
\begin{lstlisting}
instance (HList r) => Record r St where
  data Label St r a = StLabel (r->a) (r->a->r)
  data Field St a = StField a deriving (Show)
  StRef get set <== StLabel read write =
    StRef(do r <- get
             return (let (StField f) = (read r) in f))
         (\v'-> do r <- get
                   set (write r (StField v')))
\end{lstlisting}

To more easily manipulate records we define a function for building labels from $CNum$s:
\begin{lstlisting}
labelAt :: forall l n . (HList l, CNum n, HLookup l n) => n -> Label St l (HAt l n)
labelAt _ = StLabel (\l -> lookup l (undefined::n)) 
                    (\l -> \v -> update l (undefined::n) v)
\end{lstlisting}

We can now give a second example that works with records:
\begin{lstlisting}
type Person = (Field St String) `Malloc` (Field St String) `Malloc` (Field St Int) `Malloc` Nil
first :: Label St Person (Field St String)
first = labelAt Z
last :: Label St Person (Field St String)
last = labelAt (S Z)
age :: Label St Person (Field St Int)
age = labelAt (S (S Z))

mk_person :: String -> String -> Int -> Person
mk_person f l a = ((StField f) `Malloc` (StField l) `Malloc` (StField a) `Malloc` Nil)

ex2 :: (HList m0, State st, m1 ~ Malloc Person m0, Monad (st m1 m1), Record Person st) => 
            Label st Person (Field st String) -> Label st Person (Field st Int) -> st m0 m1 Person
ex2 last age = do+ p <- new (mk_person "John" "Smith" 27)
                   (p <= last) *= (++ " Jr.")
                   (p <= age) .= 25
                   eval p

res2 = runSt (ex2 last age) Nil
\end{lstlisting}
The result is, as expected, $"John"\ 'Malloc'\ "Smith Jr."\ 'Malloc'\ 25\ 'Malloc'\ Nil$.

We give one last example that does not work even though at a first glance we would expect it to. This example is used to introduce the next session:
\begin{lstlisting}
ex3_wrong :: St Nil (Malloc (Malloc Nil Int) String) Unit
ex3_wrong = do+ i <- new 10 :: Ref (New Nil Int) Int
                s <- new "Hello" :: Ref (New (New Nil Int) String) String
                i *= (+2)
                s *= (++" World")
                return ())
\end{lstlisting}

This example does not even compile because:
\begin{lstlisting}
i *= (+2) :: St (New Nil Int) (New Nil Int) ()
\end{lstlisting}

while
\begin{lstlisting}
s *= (++" World") :: St (New (New Nil Int) String) (New (New Nil Int) String) ()
\end{lstlisting}

but neither $(>>+)$ nor $(>>=)$ are capable of sequentializing these two statements. This said, it would 
not be unreasonable to expect that a statement that manipulates a smaller state such as:
\begin{lstlisting}
New Nil Int
\end{lstlisting}

could be made work with references that expect a smaller state, such as
\begin{lstlisting}
New (New Nil Int) String
\end{lstlisting}

since all the original reference needs will be passed it together with some "trailing garbage" in the form of a larger state. The notion we will use to fix this problem  happens to be that of coercive subtyping.