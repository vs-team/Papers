%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% monads refresher.tex
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

A monad \cite{MOGGI_MON,DECL_IMP,COMPR_MON,EFF_MON} is a kind of
abstract data type used to represent computations (instead of data in
the domain model). Monads allow the programmer to chain actions
together to build a pipeline; the monad stores and represents
additional processing rules which decorate each action. Monads in the
literature have been used to model stateful programs, to manipulate
sequences, asynchronous computations and many more. 

What is most interesting about monads is that they allow us to
customize the way that operations are chained together. For this
reason a monad is capable of capturing extremely complex patterns that
could otherwise only be described informally and enforced manually by
the user. The monad acts as a framework and manages all the undercover
work required by the computation.  

Formally, a monad is constructed by defining a type constructor $M$
and two operators (\texttt{bind} and \texttt{return}) that must
fulfill several properties to allow the correct composition of monadic
functions (i.e. functions that use values from the monad as their
arguments). The \texttt{return} operation takes a value from a plain
type and puts it into a monadic container of type $M$. The
\texttt{bind} operation performs the reverse process, extracting the
original value from the container and passing it to the associated
next function in the pipeline. 

As a first example, let us consider the simple ``maybe monad'', which
represents computations which may fail depending on runtime
conditions. The data constructor $M$ is the standard $\mathtt{Maybe}$
type defined below: 
\begin{lstlisting}
type Maybe<'a> = Some of 'a | None
\end{lstlisting}

In F\# we define a helper class (called a ``monad builder'') that has
the bind and return operators as methods. In the case of the maybe
monad, we represent operations that can either hold some result or no
result in case of error. Creating such an operation from a value $x$
simply packs $x$ into $\mathtt{Some}$; binding an operation $p$ with another
operation $k$ \textit{which depends on the result of $p$} requires us
to inspect $p$; if $p$ contains some result $r$, then we return $k\
r$; otherwise we cannot invoke $k$ because $p$ gave an error, and so
we propagate the error by returning $\mathtt{None}$: 

\begin{lstlisting}
type MaybeBuilder() =
  member this.Bind(p, k) =
    match p with
    | None -> None
    | Some r -> k r
  member this.Return(x)  = Some x

let maybe = MaybeBuilder()
\end{lstlisting}

F\# uses some syntactic sugar to hide the invocation of the bind and
return methods; in particular, writing: 

\medskip
\begin{lstlisting}
let maybe_sum t1 t2 =
  maybe{
    let! x = t1
    let! y = t2
    return x+y
  }
\end{lstlisting}

is equivalent to writing:
\begin{lstlisting}
maybe.Bind(t1, fun x ->
  maybe.Bind(t2, fun y ->
    maybe.Return(x+y)))
\end{lstlisting}

In general the keywords $\mathtt{let!}$ and $\mathtt{do!}$
(\texttt{do!} is a shortcut to \texttt{let! \_ = }) are desugarized
into binds, while \texttt{return} is turned into a return. Another
useful and recurring keyword, \texttt{return!}, is desugarized as: 
\begin{lstlisting}
return! m = let! x = m
            return x
\end{lstlisting}

The small sample above tries to evaluate $\mathtt{t1}$ and
$\mathtt{t2}$; if any of those returns an error, then the entire
computation returns an error. If both $\mathtt{t1}$ and
$\mathtt{t2}$ succeed, then the sum of their results is returned. 

The maybe monad can be extended into a fully fledged exception
handling system by using a smarter constructor in place of 
$\mathtt{None}$, like $\mathtt{Error\ of\ String}$ or something even
more sophisticated.  

The second monad we look at is the list monad. This monad allows us to
define computations on list and sequences: 
\begin{lstlisting}
type ListBuilder() =
  member this.Bind(l, k) = 
    List.concat(List.map k l)
  member this.Return(x) = [x]

let list = ListBuilder()
\end{lstlisting}

With this powerful monad we can treat a list by binding all its
elements (one at a time) to some value and then process them; as an
example let us consider the cartesian product between two lists 
$\mathtt{l1}$ and $\mathtt{l2}$: 
\begin{lstlisting}
let cartesian l1 l2 = 
  list { 
    let! x = l1
    let! y = l2
    return (x,y)
  }
\end{lstlisting}

The final monad we consider is the state monad, which allows us to
define and represent stateful operations in a pure functional
language; the type of the monad represents a statement where
$\mathtt{'s}$ is the state and $\mathtt{'a}$ is the type of the result
of the statement:  
\begin{lstlisting}
type State<'s, 'a> = 's ->'a * 's
\end{lstlisting}

Binding propagates the state across sequential statements, so that
each statement gets as input the state returned by the previous
statement: 
\begin{lstlisting}
type StateBuilder() =         
  member this.Bind(p : State<'s,'a>, 
                   k : 'a -> State<'s,'b>) 
           : State<'s,'b> = 
    fun s ->
      let res,s' = p s
      in k res s'
  member this.Return(x) : State<'s,'a> = 
    fun s -> x,s      

let state = StateBuilder()
\end{lstlisting}

Let us build a sample state that contains only a pair of integers; we
define ``get'' and ``set'' statements  that allow us to access the two
fields of the state: 
\begin{lstlisting}
let get_x = fun s -> fst s,s
let set_x x' = fun (x,y) -> (),(x',y)
let get_y = fun s -> snd s,s
let set_y y' = fun (x,y) -> (),(x,y')
\end{lstlisting}

At this point we could define an operation which reads the two values
from the state and stores their product in the second item: 
\begin{lstlisting}
let mul = 
  state{
    let! x = get_x
    let! y = get_y
    do! set_y (x*y)
  }
\end{lstlisting}

In the next section we will use a specialized variation of the state monad to define scripts.