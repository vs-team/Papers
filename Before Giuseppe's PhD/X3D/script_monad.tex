%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% script_monad.tex
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

In this section we will define a monad that will act as the runtime of our scripting system.  Having a safe, yet powerful language for defining scripts gives us the capability of adding complex logics (AI, physics, etc.) to our scenes, thereby making those scenes far more immersive and entertaining.

The main problem with a scripting system is that we want it to run
asynchronously with respect to the main loop implementing the
engine. Let us consider an operation in a game which we might want to update with a busy loop:  

\begin{lstlisting}
wait_destroyed_asteroids_greater 10
\end{lstlisting}

If this operation was defined as a recursive function which loops
until a certain condition is met (namely that \texttt{10} asteroids
are destroyed) then running this function would require its own thread
separate from the main loop. If this operation accesses the same state
that is being manipulated by the main loop, then we risk interferences
between the two threads. A much better approach would be to execute
this operation across various iterations of the main loop; executing
this loop into just one call of the \texttt{update} function would
make no sense, because unless the number of destroyed asteroids is
already greater than $10$, then we would loop forever. 

The monad we define performs a very simple trick. At every bind, it will \textit{suspend} itself and return its continuation as a 
lambda. The monad type is: 

\begin{lstlisting}
type Script<'a,'s> = 's -> Step<'a,'s>
 and Step<'a,'s> = Done of 'a 
                 | Next of Script<'a,'s>
\end{lstlisting}

Notice that the signature that is very similar to that of the regular
state monad, but rather than returning a result of type \texttt{'a}
it returns either \texttt{Done\ of\ 'a} or the continuation 
$\mathtt{Next\ of\ Script<}$\texttt{'a,'s}$\mathtt{>}$. The current
state of the computation, comprised of all the intermediate values
that are in scope at the given point, is stored as the
\textit{closure} of the lambda expression of an script value.  

Returning a result in this monad is simple: we just wrap it in the
$\mathtt{Done}$ constructor since obtaining this value  requires no
actual computation steps. Binding together two statements is a bit
trickier. We try executing the first statement; if the result is
$\mathtt{Done\ x}$ for some  $\mathtt{x}$, then we return
$\mathtt{Next(k\ x)}$, that is we perform the binding and we 
will continue with the rest of the program with the result of the first
statement plugged in. If the result is \texttt{Next\ p'}, then we cannot
invoke $\mathtt{k}$ because we have no value to pass it since we need 
$\mathtt{p}$ to complete to have a value of type \texttt{'a}. This means
that we have to bind \texttt{p'} to \texttt{k}, so that at the next
execution step we will continue the execution of \texttt{p} from where
it stopped.

\begin{lstlisting}
type ScriptBuilder() = 
  member this.Bind(p:Script<'a,'s>,
                   k:'a->Script<'b,'s>)
     : Script<'b,'s> =
    fun s ->
      match p s with
      | Done x -> Next(k x)
      | Next p' -> Next(this.Bind(p',k))

  member this.Return(x:'a) : Script<'a,'s> 
    = fun s -> Done x

let script = ScriptBuilder()
\end{lstlisting}

We also define a conversion operator that takes a statement of the
regular state monad and turns it into a statement of the script
monad. This scenario is useful because it allows us to use the script
monad in a game where the state is managed with the state monad or one
of its variations: 

\begin{lstlisting}
let (!) (p:St<'a,'s>) : Script<'a,'s> = 
  fun s -> Done(p s)
\end{lstlisting}

We define a game script as an instance of the $\mathtt{Script}$
datatype where the state (the \texttt{'s} type variable) is
instantiated to the script state \texttt{ScriptState}. The main loop will
now access the current state of the scripting system to step the current script forward of the game
script: 

\begin{lstlisting}
let script_state = ... (* initialize script state *)

let rec update_script (script_step:Script<Unit,GameState>) (script_state:GameState) =
  let script_step' = 
     match script_step script_state with
     | Done() -> fun _ -> Done()
     | Next k -> k
  in update script_step'
\end{lstlisting}

The update function executes a step of the script. If the script has
finished, then we create an identity script that will be called
indefinitely. When an iteration of the update loop is completed, then
we call update with the next state of the script as its parameter. 

At this point we can see a sample script that every \texttt{10}
asteroids destroyed makes an asteroid appear at the center of the
screen with a flash (a ``warp'' effect). This example makes use of
predefined stateful operations that add or remove an asteroid to the
game state (\texttt{AddAsteroid}, \texttt{RemoveAsteroid}) and which
add or remove a warp flash (\texttt{AddWarp}, \texttt{RemoveWarp}) to
the game state. The warp flashes created with the \texttt{mk\_warp}
function are animated, so that they do not suddenly appear but rather
they appear extremely small before quickly growing in size; the
created object (in our case the asteroid) appears behind a fully grown
flash which then quickly shrinks until finally it disappears. The
animation of a warp flash is performed by the recursive
\texttt{warp\_animation} function:

\begin{lstlisting}
let warp_animation src dst max_dt (a:Entity) =
  let rec warp t0 = 
    script{
      let! t = time
      let dt = t - t0
      if dt < max_dt then
        let size = interpolate src dst 
                        (dt / max_dt)
        do! !(SetSize w size)
        return! warp t0
    }
  in script{
    let! t0 = time
    do! warp t0
  }

let rec warp_script n =
  script{
    do! wait_destroyed_asteroids n
    let a = mk_random_asteroid
    let p = a.position
    let w = mk_warp p
    do! !(AddWarp w)
    do! warp_animation 0.0f WARP_SIZE WARP_IN_DURATION a
    do! !(AddAsteroid a)
    do! warp_animation WARP_SIZE 0.0f WARP_OUT_DURATION a
    do! !(RemoveWarp w)
    do! wait 5.0f
    let w = mk_warp a.position
    do! !(AddWarp w)
    do! warp_animation 0.0f WARP_SIZE WARP_IN_DURATION a
    do! !(RemoveAsteroid a)
    do! warp_animation WARP_SIZE 0.0f WARP_OUT_DURATION a
    do! !(RemoveWarp w)
    let! t0 = time
    return! warp_script (n+10) 
  }
\end{lstlisting}

Here, and throughout we use the standard F\# convention that missing
\texttt{else} branches correspond to \texttt{else} branches with
\texttt{return ()} as the body. Similarly, we use the infix
application operator \texttt{|>}, writing \texttt{x |> f} as an
equivalent for \texttt{(f x)}. 

Notice that even though the above code contains many
loops (in \texttt{warp\_animation} and \texttt{wait}), the monadic
bindings ensure that loop steps are interleaved with the game engine
loop, as required to preserve the expected interactive nature of the game. 
In addition, it is important to realize that writing such code
\textit{without this monad} would require explicitly representing 
the current state of the computation so that the next step can be
performed at the next update call: readability and correctness would
be greatly diminished.

We have built a library of combinators to be used with our scripting system. These combinators allow for parallel execution of scripts, looping and various other control structures. For more information, see \cite{UNFOLD_MONAD}.
