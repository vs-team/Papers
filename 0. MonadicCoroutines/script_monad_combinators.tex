%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% script_monad_combinators.tex
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

The script monad is the runtime core of our DSL. A DSL can be augmented by defining a series of operators that automate or simplify common operations for the DSL developers. The best set of operators for a scripting DSL is strongly dependent upon the kind of game to be scripted. In this section we describe a general-purpose set of operators that make up a basic calculus of coroutines, but we would expect that other game developers would define additional operators that are a tighter fit to their games. The operators of our calculus of coroutines take as input one or more coroutines and return as output a new coroutine:

\begin{itemize}
\item \texttt{parallel} ($s_1 \wedge s_2$) executes two scripts in parallel and returns both results
\item \texttt{concurrent} ($s_1 \vee s_2$) executes two scripts concurrently and returns the result of the first to terminate
\item \texttt{guard} ($s_1 \Rightarrow s_2$) executes and returns the result of a script only when another script evaluates to \texttt{true}
\item \texttt{repeat} ($\uparrow s$) keeps executing a script over and over
\item \texttt{atomic} ($\downarrow s$) forces a script to run in a single tick of the \textit{discrete simulation engine}
\end{itemize}

We show here the implementation of one of these combinators with our monadic system. To see the other implementations, see \cite{FRIENDLY_FSHARP}.

\begin{lstlisting}
let rec parallel_ (s1:Script<'a>) (s2:Script<'b>) 
          : Script<'a * 'b> =
  fun s ->
    match s1 s,s2 s with
    | Done x, Done y    -> Done (x,y)
    | Next k1, Next k2  -> parallel_ k1 k2
    | Next k1, Done y   -> parallel_ k1 (fun s -> Done y)
    | Done x, Next k2   -> parallel_ (fun s -> Done x) k2
\end{lstlisting}


We can now give another self-contained example that shows a producer and a consumer running in parallel. We start by defining the state as a single memory location, which can either be empty (\texttt{None}) or contain a value (\texttt{Some x}):

\begin{lstlisting}
type Buffer = { mutable Contents : Option<int> }
let buffer = { Contents = None }
\end{lstlisting}

We define some additional helper functions to access the state. A good engineering rule is that the more complex is the state, the less coroutines directly use the \texttt{get\_state} function; rather, when the state is complex, we define a series of additional accessor functions that help us manipulating the various aspects of the state:

\begin{lstlisting}
let set_buffer v : Script<Unit,Buffer> = 
  script{
    let! s = get_state
    s.Contents <- Some v }
let is_buffer_empty : Script<bool,Buffer> = 
  script{
    let! s = get_state
    return s.Contents = None }
\end{lstlisting}

We define the producer (the consumer is symmetric) as follows; notice that each access to the state automatically suspends the coroutine, so we do not need to explicitly invoke \texttt{suspend}:

\begin{lstlisting}
let rec wait_buffer_empty:Script<Unit,Buffer> = 
  script{
    let! c = is_buffer_empty
    if c = false then
      do! suspend
      do! wait_buffer_empty }

let producer =
  let rec producer i : Script<Unit,Buffer> =
    script{
      do! wait_buffer_empty
      do! set_buffer i
      do! producer (i+1) }
\end{lstlisting}

The main loop is very similar to the main loop seen for the Fibonacci sample above; the only difference is that we invoke the producer and the consumer coroutines in parallel, by passing to the inner \texttt{main\_loop} function the value \texttt{parallel\_ producer consumer}.


\subsection{Scripting in Games}

We now discuss the introduction of our scripting system in games. In the following we outline how we have built most of the game logic of the upcoming RTS game Galaxy Wars (which is also released with a fully open source \cite{GALAXY_WARS}). In the game we are considering the players compete to conquer a series of \textit{star systems} by sending fleets to reinforce their systems or to conquer the opponent's.

\subsubsection{Game Patterns}

Thanks to our general combinators we can define a small set of recurring game patterns; by instantiating these game patterns one can build the actual game scripts with great ease. These patterns may be adapted for the specific domain of a game, or altogether new patterns may be created that better fit one's game. 
The first game pattern we see is the most general, and for this reason it is called \texttt{game\_pattern}. This pattern initializes the game in a single tick, then performs a game logic (while the game is not over) and finally it performs the ending operation before returning some result. The initialization is performed by the \texttt{init} script, which returns a result of a generic type \texttt{'a}; this result is the state of the script, and contains data that may be helpful for tracking additional information that is useful to our scripts but which is not stored in the game state. The logic of the various game entities, such as their AI, is then performed, repeatedly, by the \texttt{logic} script. While the logic script is run, the \texttt{game\_over} script continuously checks to see if the game has been won or lost and thus must be terminated; when the termination condition is met, the \texttt{ending} script is invoked that may show some recap of the game that has just ended. The game pattern is implemented as follows:

\begin{lstlisting}
let game_pattern 
      (init:Script<'a>) 
      (game_over:'a -> Script<'bool>) 
      (logic:'a -> Script<Unit>) 
      (ending:'a -> Script<'c>) 
      : Script<'c> =
  script{
    let! x = init |> atomic_
    let! (Left y) = 
       concurrent_ 
         (guard_ (game_over x) (ending x)) 
         (logic x |> repeat_)
    return y }
\end{lstlisting}

Notice that with the introduction of appropriate operators we may remove many of the parentheses of the above sample if they are seen as a hindrance to readability. The main portion of the code above may then be rewritten as:

\begin{lstlisting}
(game_over x => ending x) .|| (logic x |> repeat_)
\end{lstlisting}

The game pattern above is very general, but not all scripts always need all of its parameters. We can build less general game patterns by reducing the number of parameters; for example, we may build a game pattern that has no initialization, logic or ending sequence; such a game pattern would implement the case of a game script whose sole responsibility is to check the termination conditions for a game (those that trigger the ``game over'' screen):

\begin{lstlisting}
let wait_game_over (game_over:Script<bool>) : Script<Unit> = 
  let null_script = script{ return () }
  game_pattern 
    null_script
    (fun () -> game_over)
    (fun () -> null_script)
    (fun () -> null_script)
\end{lstlisting}

Writing a script with our system will consist of instantiating one game pattern with specialized scripts as its parameters; these scripts will alternate accesses to the specific state of the game with invocations of combinators from the calculus seen above. In the next session we will see an example of this.
